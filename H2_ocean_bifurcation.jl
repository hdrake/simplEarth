### A Pluto.jl notebook ###
# v0.12.10

using Markdown
using InteractiveUtils

# This Pluto notebook uses @bind for interactivity. When running this notebook outside of Pluto, the following 'mock version' of @bind gives bound variables a default value (instead of an error).
macro bind(def, element)
    quote
        local el = $(esc(element))
        global $(esc(def)) = Core.applicable(Base.get, el) ? Base.get(el) : missing
        el
    end
end

# â•”â•â•¡ 9c8a7e5a-12dd-11eb-1b99-cd1d52aefa1d
begin
	import Pkg
	Pkg.activate(mktempdir())
	Pkg.add([
		"Plots",
		"PlutoUI",
		"Images",
		"FileIO",
		"ImageMagick",
		"ImageIO",
		"OffsetArrays",
		"ThreadsX",
		"BenchmarkTools",
	])
	using Statistics
	using Plots
	using PlutoUI
	using Images
	using OffsetArrays
	using ThreadsX
	using BenchmarkTools
end

# â•”â•â•¡ 67c3dcc0-2c05-11eb-3a84-9dfea24f95a8
md"_homework 10, version 0_"

# â•”â•â•¡ 621230b0-2c05-11eb-2a98-5bd1d7be9038
md"""

# **Homework 10**: _Climate modeling II_
`18.S191`, fall 2020
"""

# â•”â•â•¡ 6cb238d0-2c05-11eb-221e-d5df4c479302
# edit the code below to set your name and kerberos ID (i.e. email without @mit.edu)

student = (name = "Jazzy Doe", kerberos_id = "jazz")

# you might need to wait until all other cells in this notebook have completed running. 
# scroll around the page to see what's up

# â•”â•â•¡ 6a4641e0-2c05-11eb-3430-6f14650c2ad3
md"""

Submission by: **_$(student.name)_** ($(student.kerberos_id)@mit.edu)
"""

# â•”â•â•¡ 70077e50-2c05-11eb-3d83-732b4b780d04
md"_Let's create a package environment:_"

# â•”â•â•¡ ed741ec6-1f75-11eb-03be-ad6284abaab8
html"""
<iframe width="100%" height="300" src="https://www.youtube.com/embed/waOzCGDNPzk" frameborder="0" allow="accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture" allowfullscreen></iframe>
"""

# â•”â•â•¡ 68c01d90-2cf6-11eb-0771-7b3c6db89ecb
md"""
In Lecture 23 (video above), we looked at a 2D ocean model that included two physical processes: **advection** (_flow of heat_) and **diffusion** (_spreading of heat_). This homework includes the model from the lecture, and you will be able to experiment with it yourself! 

The model is written in a way that it can be **extended with more physical processes**. In this homework we will add two more effects, introduced in the Energy Balance Model from our last homework: _absorbed_ and _emitted_ radiation. 
"""

# â•”â•â•¡ 295af330-2cf8-11eb-1606-437e8f3c43fd
md"""
## **Exercise 1** - _Advection-diffusion_

Included below is the two-dimensional advection-diffusion model from Lecture 23. To keep this homework concise, we have only included the code. To see the original notebook with more detailed comments, use the link below:
"""

# â•”â•â•¡ c33ebe40-2cf9-11eb-384c-432dc70497b0
let
	# We reference Pkg because running Pkg operations in two notebooks at
	# the same time will break Pkg. By referencing it, this link will only show
	# when Pkg is done.
	Pkg 
	
	# This link was generated by calling
	# encodeURIComponent("the actual link")
	# in javascript (press F12 and look for the console)
	html"""<blockquote><p>Click <a href="./open?url=https%3A%2F%2Fraw.githubusercontent.com%2Fhdrake%2FsimplEarth%2Fmaster%2F4_ocean_heat_transport.jl" target="_blank">here</a> to download and run the Lecture 23 notebook in a new tab.</p></blockquote>"""
end

# â•”â•â•¡ 83ad05a0-2cfb-11eb-1467-e1196985519a
md"""
#### Advection & diffusion

Notive that both functions have a main method with the following signature:

`(::Array{Float64,2}, ::ClimateModel)` _maps to_ `::Array{Float64,2}`.

As we will see later, `ClimateModel` contains the grid, the velocity vector field and the simulation parameters.
"""

# â•”â•â•¡ 1e8d37ee-2a97-11eb-1d45-6b426b25d4eb
xgrad_kernel = OffsetArray(reshape([-1., 0, 1.], 1, 3),  0:0, -1:1);

# â•”â•â•¡ 682f2530-2a97-11eb-3ee6-99a7c79b3767
ygrad_kernel = OffsetArray(reshape([-1., 0, 1.], 3, 1),  -1:1, 0:0);

# â•”â•â•¡ b629d89a-2a95-11eb-2f27-3dfa45789be4
xdiff_kernel = OffsetArray(reshape([1., -2., 1.], 1, 3),  0:0, -1:1);

# â•”â•â•¡ a8d8f8d2-2cfa-11eb-3c3e-d54f7b32e4a2
ydiff_kernel = OffsetArray(reshape([1., -2., 1.], 3, 1),  -1:1, 0:0);

# â•”â•â•¡ cd2ee4ca-2a06-11eb-0e61-e9a2ecf72bd6
begin
	struct Grid
		N::Int64
		L::Float64

		Î”x::Float64
		Î”y::Float64

		x::Array{Float64, 2}
		y::Array{Float64, 2}

		Nx::Int64
		Ny::Int64

		# constructor function:
		function Grid(N, L)
			Î”x = L/N # [m]
			Î”y = L/N # [m]

			x = 0. -Î”x/2.:Î”x:L +Î”x/2.
			x = reshape(x, (1, size(x,1)))
			y = -L -Î”y/2.:Î”y:L +Î”y/2.
			y = reshape(y, (size(y,1), 1))

			Nx, Ny = size(x, 2), size(y, 1)

			return new(N, L, Î”x, Î”y, x, y, Nx, Ny)
		end
	end

	Base.zeros(G::Grid) = zeros(G.Ny, G.Nx)
end

# â•”â•â•¡ e7f563f0-2d04-11eb-036d-992da68470a6
Grid(5,300.0e3)

# â•”â•â•¡ 39404240-2cfe-11eb-2e3c-710e37f8cd4b
md"""
Next, let's look at three new types. Two structs: `OceanModel` and `OceanModelParameters`, and an abstract type: `ClimateModel`.
"""

# â•”â•â•¡ 0d63e6b2-2b49-11eb-3413-43977d299d90
Base.@kwdef struct OceanModelParameters
	
	Îº::Float64=1.e4
end

# â•”â•â•¡ 32663184-2a81-11eb-0dd1-dd1e10ed9ec6
abstract type ClimateModel end

# â•”â•â•¡ f4c884fc-2a97-11eb-1ba9-01bf579f8b43
begin
	# main method:
	advect(T::Array{Float64,2}, O::ClimateModel) = 
		advect(T, O.u, O.v, O.G.Î”y, O.G.Î”x)
	
	# helper methods:
	advect(T::Array{Float64,2}, u, v, Î”y, Î”x) = [
		advect(T, u, v, Î”y, Î”x, j, i)
		for j=2:size(T, 1)-1, i=2:size(T, 2)-1
	]
	
	advect(T::Array{Float64,2}, u, v, Î”y, Î”x, j, i) = .-(
		u[j, i].*sum(xgrad_kernel[0, -1:1].*T[j, i-1:i+1])/(2Î”x) .+
		v[j, i].*sum(ygrad_kernel[-1:1, 0].*T[j-1:j+1, i])/(2Î”y)
	)
end

# â•”â•â•¡ ee6716c8-2a95-11eb-3a00-319ee69dd37f
begin
	# main method:
	diffuse(T::Array{Float64,2}, O::ClimateModel) = 
		diffuse(T, O.params.Îº, O.G.Î”y, O.G.Î”x)
	
	# helper methods:
	diffuse(T, Îº, Î”y, Î”x) = [
		diffuse(T, Îº, Î”y, Î”x, j, i) for j=2:size(T, 1)-1, i=2:size(T, 2)-1
	]
	diffuse(T, Îº, Î”y, Î”x, j, i) = Îº.*(
		sum(xdiff_kernel[0, -1:1].*T[j, i-1:i+1])/(Î”x^2) +
		sum(ydiff_kernel[-1:1, 0].*T[j-1:j+1, i])/(Î”y^2)
	)
end

# â•”â•â•¡ d3796644-2a05-11eb-11b8-87b6e8c311f9
begin
	struct OceanModel <: ClimateModel
		G::Grid
		params::OceanModelParameters

		u::Array{Float64, 2}
		v::Array{Float64, 2}
	end

	OceanModel(G::Grid, params::OceanModelParameters) = 
		OceanModel(G, params, zeros(G), zeros(G))
	
	OceanModel(G::Grid) = 
		OceanModel(G, OceanModelParameters(), zeros(G), zeros(G))
end;

# â•”â•â•¡ 5f5e4120-2cfe-11eb-1fa7-99fdd734f7a7
OceanModel <: ClimateModel

# â•”â•â•¡ 74aa7512-2a9c-11eb-118c-c7a5b60eac1b
md"""
#### Timestepping


"""

# â•”â•â•¡ f92086c4-2a74-11eb-3c72-a1096667183b
begin
	mutable struct ClimateModelSimulation{ModelType<:ClimateModel}
		model::ModelType
		
		T::Array{Float64, 2}
		Î”t::Float64
		
		iteration::Int64
	end
	
	ClimateModelSimulation(C::ModelType, T, Î”t) where ModelType = 
		ClimateModelSimulation{ModelType}(C, T, Î”t, 0)
end

# â•”â•â•¡ 7caca2fa-2a9a-11eb-373f-156a459a1637
function update_ghostcells!(A::Array{Float64,2}; option="no-flux")
	Atmp = @view A[:,:]
	if option=="no-flux"
		A[1, :] = Atmp[2, :]; Atmp[end, :] = Atmp[end-1, :]
		A[:, 1] = Atmp[:, 2]; Atmp[:, end] = Atmp[:, end-1]
	end
end

# â•”â•â•¡ 81bb6a4a-2a9c-11eb-38bb-f7701c79afa2
function timestep!(sim::ClimateModelSimulation{OceanModel})
	update_ghostcells!(sim.T)
	tendencies = advect(sim.T, sim.model) .+ diffuse(sim.T, sim.model)
	sim.T[2:end-1, 2:end-1] .+= sim.Î”t*tendencies
	sim.iteration += 1
end;

# â•”â•â•¡ 31cb0c2c-2a9a-11eb-10ba-d90a00d8e03a
md"""
##### 3) Simulating heat transport by advective & diffusive ocean currents
"""

# â•”â•â•¡ 9841ff20-2c06-11eb-3c4c-c34e465e1594
default_grid = Grid(10, 6000.0e3);

# â•”â•â•¡ 31cb7aae-2d04-11eb-30cb-a365a6a4aa6b
md"""
Uncomment (`Ctrl+/` or `Cmd+/`) one of the lines below two choose a different velocity field.
"""

# â•”â•â•¡ 50e89130-2d04-11eb-1c8e-a34775aec40c
md"""
Choose the initial temperature state:
"""

# â•”â•â•¡ c7736640-2d04-11eb-1108-59a7446e244d
md"""
We define our ocean simulation. Run this cell again to reset the simulation to the initial state.
"""

# â•”â•â•¡ 981ef38a-2a8b-11eb-08be-b94be2924366
md"**Simulation controls**"

# â•”â•â•¡ d042d25a-2a62-11eb-33fe-65494bb2fad5
begin
	quiverBox = @bind show_quiver CheckBox(default=false)
	anomalyBox = @bind show_anomaly CheckBox(default=false)
	md"""
	*Click to show the velocity field* $(quiverBox) *or to show temperature **anomalies** instead of absolute values* $(anomalyBox)
	"""
end

# â•”â•â•¡ c20b0e00-2a8a-11eb-045d-9db88411746f
begin
	U_ex_Slider = @bind U_ex Slider(-4:1:8, default=0, show_value=false);
	md"""
	$(U_ex_Slider)
	"""
end

# â•”â•â•¡ 6dbc3d34-2a89-11eb-2c80-75459a8e237a
begin
	md"*Vary the current speed U:*  $(2. ^U_ex) [Ã— reference]"
end

# â•”â•â•¡ 933d42fa-2a67-11eb-07de-61cab7567d7d
begin
	Îº_ex_Slider = @bind Îº_ex Slider(0.:1.e3:1.e5, default=1.e4, show_value=true)
	md"""
	*Vary the diffusivity Îº:* $(Îº_ex_Slider) [mÂ²/s]
	"""
end

# â•”â•â•¡ c9ea0f72-2a67-11eb-20ba-376ca9c8014f
@bind go_ex Clock(0.1)

# â•”â•â•¡ c3f086f4-2a9a-11eb-0978-27532cbecebf
md"""
**Some unit tests for verification**
"""

# â•”â•â•¡ c0298712-2a88-11eb-09af-bf2c39167aa6
md"""##### Computing the velocity field for a single circular vortex
"""

# â•”â•â•¡ e3ee80c0-12dd-11eb-110a-c336bb978c51
begin
	âˆ‚x(Ï•, Î”x) = (Ï•[:,2:end] - Ï•[:,1:end-1])/Î”x
	âˆ‚y(Ï•, Î”y) = (Ï•[2:end,:] - Ï•[1:end-1,:])/Î”y
	
	xpad(Ï•) = hcat(zeros(size(Ï•,1)), Ï•, zeros(size(Ï•,1)))
	ypad(Ï•) = vcat(zeros(size(Ï•,2))', Ï•, zeros(size(Ï•,2))')
	
	xitp(Ï•) = 0.5*(Ï•[:,2:end]+Ï•[:,1:end-1])
	yitp(Ï•) = 0.5*(Ï•[2:end,:]+Ï•[1:end-1,:])
	
	function diagnose_velocities(Ïˆ, G)
		u = xitp(âˆ‚y(Ïˆ, G.Î”y/G.L))
		v = yitp(-âˆ‚x(Ïˆ, G.Î”x/G.L))
		return u,v
	end
end

# â•”â•â•¡ df706ebc-2a63-11eb-0b09-fd9f151cb5a8
function impose_no_flux!(u, v)
	u[1,:] .= 0.; v[1,:] .= 0.;
	u[end,:] .= 0.; v[end,:] .= 0.;
	u[:,1] .= 0.; v[:,1] .= 0.;
	u[:,end] .= 0.; v[:,end] .= 0.;
end

# â•”â•â•¡ e2e4cfac-2a63-11eb-1b7f-9d8d5d304b43
function PointVortex(G; Î©=1., a=0.2, x0=0.5, y0=0.)
	x = reshape(0. -G.Î”x/(G.L):G.Î”x/G.L:1. +G.Î”x/(G.L), (1, G.Nx+1))
	y = reshape(-1. -G.Î”y/(G.L):G.Î”y/G.L:1. +G.Î”y/(G.L), (G.Ny+1, 1))
	
	function ÏˆÌ‚(x,y)
		r = sqrt.((y .-y0).^2 .+ (x .-x0).^2)
		
		stream = -Î©/4*r.^2
		stream[r .> a] = -Î©*a^2/4*(1. .+ 2*log.(r[r .> a]/a))
		
		return stream
	end
		
	u, v = diagnose_velocities(ÏˆÌ‚(x, y), G)
	impose_no_flux!(u, v)
	
	return u,v
end

# â•”â•â•¡ 1dd3fc70-2c06-11eb-27fe-f325ca208504
# ocean_velocities = zeros(default_grid), zeros(default_grid);
ocean_velocities = PointVortex(default_grid, Î©=0.5);
# ocean_velocities = DoubleGyre(default_grid);

# â•”â•â•¡ bb084ace-12e2-11eb-2dfc-111e90eabfdd
md"""##### Computing a quasi-realistic ocean velocity field $\vec{u} = (u, v)$
Our velocity field is given by an analytical solution to the classic wind-driven gyre
problem, which is given by solving the fourth-order partial differential equation:

$- \epsilon_{M} \hat{\nabla}^{4} \hat{\Psi} + \frac{\partial \hat{\Psi} }{ \partial \hat{x}} = \nabla \times \hat{\tau} \mathbf{z},$

where the hats denote that all of the variables have been non-dimensionalized and all of their constant coefficients have been bundles into the single parameter $\epsilon_{M} \equiv \dfrac{\nu}{\beta L^3}$.

The solution makes use of an advanced *asymptotic method* (valid in the limit that $\epsilon \ll 1$) known as *boundary layer analysis* (see MIT course 18.305 to learn more). 
"""



# â•”â•â•¡ ecaab27e-2a16-11eb-0e99-87c91e659cf3
function DoubleGyre(G; Î²=2e-11, Ï„â‚€=0.1, Ïâ‚€=1.e3, Î½=1.e5, Îº=1.e5, H=1000.)
	ÏµM = Î½/(Î²*G.L^3)
	Ïµ = ÏµM^(1/3.)
	x = reshape(0. -G.Î”x/(G.L):G.Î”x/G.L:1. +G.Î”x/(G.L), (1, G.Nx+1))
	y = reshape(-1. -G.Î”y/(G.L):G.Î”y/G.L:1. +G.Î”y/(G.L), (G.Ny+1, 1))
	
	ÏˆÌ‚(x,y) = Ï€*sin.(Ï€*y) * (
		1 .- x - exp.(-x/(2*Ïµ)) .* (
			cos.(âˆš3*x/(2*Ïµ)) .+
			(1. /âˆš3)*sin.(âˆš3*x/(2*Ïµ))
			)
		.+ Ïµ*exp.((x .- 1.)/Ïµ)
	)
		
	u, v = (Ï„â‚€/Ïâ‚€)/(Î²*G.L*H) .* diagnose_velocities(ÏˆÌ‚(x, y), G)
	impose_no_flux!(u, v)
	
	return u, v
end

# â•”â•â•¡ e59d869c-2a88-11eb-2511-5d5b4b380b80
md"""
##### Some simple initial temperature fields
"""

# â•”â•â•¡ 0ae0bb70-2b8f-11eb-0104-93aa0e1c7a72
constantT(G; value) = zeros(G) .+ value

# â•”â•â•¡ c4424838-12e2-11eb-25eb-058344b39c8b
linearT(G; value=50.0) = value*0.5*(1. .+[ -(y/G.L) for y in G.y[:, 1], x in G.x[1, :] ])

# â•”â•â•¡ 3d12c114-2a0a-11eb-131e-d1a39b4f440b
function InitBox(G; value=50., nx=2, ny=2, xspan=false, yspan=false)
	T = zeros(G)
	T[G.NyÃ·2-ny:G.NyÃ·2+ny, G.NxÃ·2-nx:G.NxÃ·2+nx] .= value
	if xspan
		T[G.NyÃ·2-ny:G.NyÃ·2+ny, :] .= value
	end
	if yspan
		T[:, G.NxÃ·2-nx:G.NxÃ·2+nx] .= value
	end
	return T
end

# â•”â•â•¡ 6f19cd80-2c06-11eb-278d-178c1590856f
# ocean_T_init = InitBox(default_grid; value=40);
ocean_T_init = InitBox(default_grid, value=50, xspan=true);
# ocean_T_init = linearT(default_grid); 

# â•”â•â•¡ 863a6330-2a08-11eb-3992-c3db439fb624
ocean_sim = let
	P = OceanModelParameters(Îº=Îº_ex)
	
	u, v = ocean_velocities
	model = OceanModel(default_grid, P, u*2. ^U_ex, v*2. ^U_ex)
	
	Î”t = 12*60*60
	ClimateModelSimulation(model, copy(ocean_T_init), Î”t)
end;

# â•”â•â•¡ dc9d12d0-2a9a-11eb-3dae-85b3b6029658
begin
	heat_capacity = 51.
	total_heat_content = sum(heat_capacity*ocean_sim.T*(ocean_sim.model.G.Î”x*ocean_sim.model.G.Î”y))*1e-15
	mean_temp = mean(ocean_sim.T)
end;

# â•”â•â•¡ bff89550-2a9a-11eb-3038-d70249c96219
begin
	#go_ex
	md"""
	Let's make sure our model conserves energy. We have not added any energy to the system: advection and diffusion just move the energy around. The total heat content is $(round(total_heat_content, digits=3)) peta-Joules and the average temperature is $(round(mean_temp, digits=2)) Â°C.
	"""
end

# â•”â•â•¡ 6b3b6030-2066-11eb-3343-e19284638efb
plot_kernel(A) = heatmap(
	collect(A),
	color=:bluesreds, clims=(-maximum(abs.(A)), maximum(abs.(A))), colorbar=false,
	xticks=false, yticks=false, size=(30+30*size(A, 2), 30+30*size(A, 1)), xaxis=false, yaxis=false
) |> as_svg

# â•”â•â•¡ 88c56350-2c08-11eb-14e9-77e71d749e6d
md"""
## **Exercise 2** - _Complexity_

In this class we have purposefully restricted ourself to small problems ($N_{t} < 100$ timesteps and $N_{x;\, y} < 30$ spatial grid-cells) so that they can be run interactively on an average computer. In state-of-the-art climate modelling however, the goal is to push the *numerical resolution* $N$ to be as large as possible (the *grid spacing* $\Delta t$ or $\Delta x$ as small as possible), to resolve physical processes that improve the realism of the simulation (see below).

"""

# â•”â•â•¡ 014495d6-2cda-11eb-05d7-91e5a467647e
html"""<img src="https://alps-ocean.us/wp-content/uploads/2018/03/zykov_f3.jpg" height=470>"""

# â•”â•â•¡ d6a56496-2cda-11eb-3d54-d7141a49a446
md"""
Here, we provide a simple estimate of the *computational complexity* of climate models, which reveals a substantial challenge to the improvement of climate models.

Our climate model algorithm can be summarized by the recursive formula:

$T_{i,\, j}^{n+1} = T^{n}_{i,j} + \Delta t * \left( \text{tendencies} \right)$

For a time $t_{M} = M \Delta t$, the complexity is

$\mathcal{O}(T(t_{M})) = \mathcal{O}(M) * \mathcal{O}(\text{tendencies}),$

where $M$ is the number of timesteps (assuming $\Delta t$ constant) and $\mathcal{O}(\text{tendencies})$ is the computational complexity of computing the tendency for each $i \in [1, N_{x}]$ and $j \in [1, N_{y}]$ for a single timestep. For a fixed aspect ratio $N_{y} = 2N_{x}$, our nested-for-loop implementation has a complexity

$\mathcal{O}(\text{tendencies}) = \mathcal{O}(2N_{x}^{2}).$

Thus, the computational complexity of our 2D climate model appears to be:

$\mathcal{O}(T(t_{M})) = \mathcal{O}(M) \mathcal{O}(N_{x}^{2}),$

i.e. quadratic in the spatial resolution $N_{x}$.

#### Exercise 2.1

For constant ``M``, we want to verify that $\mathcal{O}(T(t_{M})) = \mathcal{O}(N_{x}^{2})$ holds for our numerical model. 

ğŸ‘‰ Write a function `model_runtime` that takes `N` as an argument, and sets up a model with grid of resolution `N`, and returns the runtime of a single `timestep!`.

"""

# â•”â•â•¡ 126bffce-2d0b-11eb-2bfd-bb5d1ad1169b
# function runtime(N)
	
# 	return missing
# end

# â•”â•â•¡ 923af680-2d0b-11eb-3f6a-db4bf29bb6a9
md"""
ğŸ‘‰ Call your `runtime` function on a range of values for `N`, and use a plot to demonstrate that the predicted runtime complexity holds.
"""

# â•”â•â•¡ b50d25c0-2d0b-11eb-3b0c-1b178bbddecd


# â•”â•â•¡ 16905a6a-2a78-11eb-19ea-81adddc21088
Nvec = 8:8:200

# â•”â•â•¡ a6811db2-2cdf-11eb-0aac-b1bf7b7d99eb
md"""
#### Exercise 2.2 - _The CFL condition on $\Delta t$_

In Exercise 1, look for the definition of `Î”t`. It is currently set to `12*60*60` (12 hours).

ğŸ‘‰ Double `Î”t` and run the simulation again. You should see that it runs faster, great! Now, keep doubling `Î”t` until you see something 'strange'. Describe what you see.


"""

# â•”â•â•¡ 87de1c70-2d0c-11eb-2c22-f76eeca58f33
Î”t_doubling_observations = md"""

Hi!

"""

# â•”â•â•¡ ad7b7ed6-2a9c-11eb-06b7-0f5595167575
function CFL_adv(sim::ClimateModelSimulation)
	maximum(sqrt.(sim.model.u.^2 + sim.model.v.^2)) * sim.Î”t / sim.model.G.Î”x
end

# â•”â•â•¡ 433a9c1e-2ce0-11eb-319c-e9c785b080ce
md"""
#### Exercise 2.3
In practice, state-of-the-art climate models are 3-D, not 2-D. It turns out that to preserve the aspect ratio of oceanic motions, the *vertical* grid resolution should also be increased $N_{z} \propto N_{x}$, such that in reality the computational complexity of climate models is:

$\mathcal{O}(T(t_{M})) = \mathcal{O}(M) \mathcal{O}(N_{x}^4).$

This is the fundamental challenge of high-performance climate computing: to increase the resolution of the models by a factor of $2$, the model's run-time increases by a factor of $2^4 = 16$.

The figure below shows how the grid spacing of state-of-the-art climate models has decreased from $500$ km in 1990 (FAR) to $100$ km in the 2010s (AR4). In other words, grid resolution increased by a factor of $5$ in 20 years.
"""

# â•”â•â•¡ 213f65ce-2ce1-11eb-19d6-5bf5c24d7ed7
html"""

<img src="https://www.nap.edu/openbook/13430/xhtml/images/p_78.jpg" height=450>
"""

# â•”â•â•¡ d9e23a5a-2a8b-11eb-23f1-73ff28be9f12
md"**The CFL condition**

The CFL condition is defined by $\text{CFL} = \dfrac{\max\left(\sqrt{uÂ² + vÂ²}\right)Î”t}{Î”x} =$ $(round(CFL_adv(ocean_sim), digits=2))
"

# â•”â•â•¡ 545cf530-2b48-11eb-378c-3f8eeb89bcba
md"""
## **Exercise 3** - _Radiation_

Blablabllabab
"""

# â•”â•â•¡ 57535c60-2b49-11eb-07cc-ffc5b4d1f13c
Base.@kwdef struct RadiationOceanModelParameters
	Îº::Float64=3.e4
	
	C::Float64=51.0 * 60*60*24*365.25 # converted from [W*year/m^2/K] to [J/m^2/K]
	
	A::Float64=210
	B::Float64=-1.3
	
	S_mean::Float64 = 1380
	Î±0::Float64=0.3
	Î±i::Float64=0.5
	Î”T::Float64=2.0
end

# â•”â•â•¡ de7456c0-2b4b-11eb-13c8-01b196821de4
md"""
## Outgoing radiation
"""

# â•”â•â•¡ 6745f610-2b48-11eb-2f6c-79e0009dc9c3
function outgoing_thermal_radiation(T; A, B)
	A .- B .* (T)
end

# â•”â•â•¡ e80b0532-2b4b-11eb-26fa-cd09eca808bc
md"""
## Incoming radiation

The incoming solar radiation $S$
"""

# â•”â•â•¡ 42bb2f70-2b4a-11eb-1637-e50e1fad45f3
function precompute_S(grid::Grid, params::RadiationOceanModelParameters)
	[
		params.S_mean .* (1+0.5*sin((-y / grid.L) * Ï€/2))
		for y in grid.y[:], x in grid.x[:]
	]
end

# â•”â•â•¡ 90e1aa00-2b48-11eb-1a2d-8701a3069e50
begin
	struct RadiationOceanModel <: ClimateModel
		G::Grid
		params::RadiationOceanModelParameters
		
		S::Array{Float64, 2}

		u::Array{Float64, 2}
		v::Array{Float64, 2}
	end

	RadiationOceanModel(G::Grid, P::RadiationOceanModelParameters, u, v) = 
		RadiationOceanModel(G, P, precompute_S(G, P), u, v)
	RadiationOceanModel(G::Grid, P::RadiationOceanModelParameters) = 
		RadiationOceanModel(G, P, zeros(G), zeros(G))
	RadiationOceanModel(G::Grid) = 
		RadiationOceanModel(G, RadiationOceanModelParameters(), zeros(G), zeros(G))
end;

# â•”â•â•¡ a033fa20-2b49-11eb-20e0-5dd968b0c0c6
function outgoing_thermal_radiation(T, model::RadiationOceanModel)
	outgoing_thermal_radiation(T; A=model.params.A, B=model.params.B) ./ model.params.C
end

# â•”â•â•¡ 6c20ca1e-2b48-11eb-1c3c-418118408c4c
plot(
	-10:40, outgoing_thermal_radiation(-10:40, A=11, B=-0.7),
	xlabel="Temperature",
	ylabel="Outgoing radiation",
	label=nothing,
	size=(300,200)
)

# â•”â•â•¡ b99b5b00-2b4b-11eb-260e-21363d1f4a9b
hello = let
	G = Grid(10, 6.e6)
	P = RadiationOceanModelParameters()
	precompute_S(G, P)
end

# â•”â•â•¡ a3e524d0-2b55-11eb-09e2-25a968d79640
plot(hello[:,1])

# â•”â•â•¡ 388898b0-2b56-11eb-2537-c596394b9e20


# â•”â•â•¡ 629454e0-2b48-11eb-2ff0-abed400c49f9
function Î±(T::Float64; Î±0, Î±i, Î”T)
	if T < -Î”T
		return Î±i
	elseif -Î”T <= T < Î”T
		return Î±i + (Î±0-Î±i)*(T+Î”T)/(2Î”T)
	elseif Î”T <= T
		return Î±0
	end
end

# â•”â•â•¡ d63c5fe0-2b49-11eb-07fd-a7ec98af3a89
function Î±(T::Array{Float64,2}, model::RadiationOceanModel)
	Î±.(T; Î±0=model.params.Î±0, Î±i=model.params.Î±i, Î”T=model.params.Î”T)
end

# â•”â•â•¡ f2e2f820-2b49-11eb-1c6c-19ae8157b2b9
function absorbed_solar_radiation(T, model::RadiationOceanModel)
	absorption = 1.0 .- Î±(T, model)
	
	absorption .* model.S ./ 4. ./ model.params.C
end

# â•”â•â•¡ fe492480-2b4b-11eb-050e-9b9b2e2bf50f
md"""
## New timestep method
"""

# â•”â•â•¡ 068795ee-2b4c-11eb-3e58-353eb8978c1c
function timestep!(sim::ClimateModelSimulation{RadiationOceanModel})
	update_ghostcells!(sim.T)
	tendencies = 
		advect(sim.T, sim.model) .+ diffuse(sim.T, sim.model) .+ 
		(@view absorbed_solar_radiation(sim.T, sim.model)[2:end-1, 2:end-1]) .- 
		(@view outgoing_thermal_radiation(sim.T, sim.model)[2:end-1, 2:end-1])
	
	sim.T[2:end-1, 2:end-1] .+= sim.Î”t*tendencies
	
	sim.iteration += 1
end;

# â•”â•â•¡ 8346b590-2b41-11eb-0bc1-1ba79bb77dfb
function runtime(N)
	G = Grid(N, 6.e6);
	P = OceanModelParameters(1.e4);

	#u, v = DoubleGyre(G)
	#u, v = PointVortex(G, Î©=0.5)
	u, v = zeros(G), zeros(G)

	model = OceanModel(G, P, u, v)

	IC = InitBox(G)
	#IC = InitBox(G, nx=G.NxÃ·2-1)
	#IC = linearT(G)

	Î”t = 6*60*60
	S = ClimateModelSimulation(model, copy(IC), Î”t)

	return @elapsed timestep!(S)
end

# â•”â•â•¡ a8136ed0-2d0e-11eb-01b3-4101bd813faf
tvec = runtime.(Nvec)

# â•”â•â•¡ 794c2148-2a78-11eb-2756-5bd28b7726fa
begin
	plot(Nvec, tvec, xlabel="Number of Grid Cells (in x-direction)", ylabel="elapsed time per timestep [s]")
end |> as_svg

# â•”â•â•¡ ef902590-2bf7-11eb-1eb0-712b3eb3f7c1
let
	G = Grid(10, 6.e6)
	P = RadiationOceanModelParameters()
	
	#u, v = zeros(G), zeros(G)
	# u, v = PointVortex(G, Î©=0.5)
	u, v = DoubleGyre(G)

	# IC = InitBox(G; value=50.)
	# IC = InitBox(G, xspan=true)
	IC = constantT(G; value=14)
	
	model = RadiationOceanModel(G, P, u*2. ^U_ex, v*2. ^U_ex)
	Î”t = 400*60*60
	
	sim = ClimateModelSimulation(model, copy(IC), Î”t)
	
	while (
			abs(
				mean(absorbed_solar_radiation(sim.T, sim.model)) * sim.model.params.C - 
				mean(outgoing_thermal_radiation(sim.T, sim.model)) * sim.model.params.C
			) > 4.0 || sim.iteration < 1_000) && (
			sim.iteration < 6_000
			)
		for i in 1:500
			timestep!(sim)
		end
	end
	
	mean(sim.T)
end

# â•”â•â•¡ ad95c4e0-2b4a-11eb-3584-dda89970ffdf
md"""
## lets try it out
"""

# â•”â•â•¡ b059c6e0-2b4a-11eb-216a-39bb43c7b423
radiation_sim = let
	G = Grid(10, 6.e6)
	P = RadiationOceanModelParameters(S_mean=1380, A=200, Î±0=0.3, Î±i=0.4, Îº=2e4)
	
	#u, v = zeros(G), zeros(G)
	# u, v = PointVortex(G, Î©=0.5)
	u, v = DoubleGyre(G)

	# IC = InitBox(G; value=50.)
	# IC = InitBox(G, xspan=true)
	IC = constantT(G; value=0)
	
	model = RadiationOceanModel(G, P, u, v)
	Î”t = 400*60*60
	
	ClimateModelSimulation(model, copy(IC), Î”t)
end

# â•”â•â•¡ 5fd346d0-2b4d-11eb-066b-9ba9c9d97613
@bind go_radiation Clock(.1)

# â•”â•â•¡ 50c6d850-2b57-11eb-2330-1d1547219b5e
(absorbed_solar_radiation(radiation_sim.T, radiation_sim.model) |> mean) * radiation_sim.model.params.C

# â•”â•â•¡ 57dcf660-2b57-11eb-1518-b7e2e65abfcc
(outgoing_thermal_radiation(radiation_sim.T, radiation_sim.model) |> mean) * radiation_sim.model.params.C

# â•”â•â•¡ f5010a40-2b56-11eb-266a-a71b92692172
mean(radiation_sim.T)

# â•”â•â•¡ ef647620-2c01-11eb-185e-3f36f98fcfaf


# â•”â•â•¡ 127bcb0e-2c0a-11eb-23df-a75767910fcb
md"""
#### Exercise 4.1 - _Equilibrium temperature_

ğŸ‘‰ Write a function `eq_T` that takes two argments, `S` and `T_init`, that sets up a radiation ocean model with `S` as `S_mean`, and with `T_init` as the constant initial temperature. Run the model until you have reached equilibrium (approximately), and return the average temperature.
"""

# â•”â•â•¡ c40870d0-2b8e-11eb-0fa6-d7fcb1c6611b
function eq_T(S, T_init)
	G = Grid(10, 6.e6)
	P = RadiationOceanModelParameters(Îº=3e4, S_mean=S, Î±i=.5, A=210)
	
	#u, v = zeros(G), zeros(G)
	# u, v = PointVortex(G, Î©=0.5)
	u, v = DoubleGyre(G)

	# IC = InitBox(G; value=50.)
	# IC = InitBox(G, xspan=true)
	IC = constantT(G; value=T_init)
	
	model = RadiationOceanModel(G, P, u*2. ^U_ex, v*2. ^U_ex)
	Î”t = 400*60*60
	
	sim = ClimateModelSimulation(model, copy(IC), Î”t)
	
	while (
			abs(
				mean(absorbed_solar_radiation(sim.T, sim.model)) * sim.model.params.C - 
				mean(outgoing_thermal_radiation(sim.T, sim.model)) * sim.model.params.C
			) > 8.0 || sim.iteration < 1_000) && (
			sim.iteration < 6_000
			)
		for i in 1:500
			timestep!(sim)
		end
	end
	
	mean(sim.T)
end

# â•”â•â•¡ 2ef83140-2d16-11eb-1d8c-a13048a8e04f
md"""
#### Exercise 4.2
"""

# â•”â•â•¡ 2ae47330-2d15-11eb-1d2e-55343fa3b01a
md"""
ğŸ‘‰ For ``S=1380`` (present-day value), does `T_init=-50` give a different result than `T_init=+50`? What about `T_ini+55`? By trying various values for `T_init`, **how many stable states do you find?**
"""

# â•”â•â•¡ 5294aad0-2d15-11eb-091d-59d7517c4dc2
S_1380_stable_states = md"""

I found ...

"""

# â•”â•â•¡ abd2475e-2d15-11eb-26dc-05253cf65232
md"""
ğŸ‘‰ Answer the same question for ``S=1000`` and ``S=2000``.
"""

# â•”â•â•¡ bdd86250-2d15-11eb-0b62-7903ca714312
S_1000_stable_states = md"""

I found ...

"""

# â•”â•â•¡ bed2c7e0-2d15-11eb-14e3-93f5d1b6f3a1
S_2000_stable_states = md"""

I found ...

"""

# â•”â•â•¡ c40b7360-2d15-11eb-0558-d95d615f9b9b
md"""
ğŸ‘‰ Use the interactive simulation to check these simulations manually. Why is the number of stable states different for a lower or higher value of ``S``?
"""

# â•”â•â•¡ 02767460-2d16-11eb-2074-07657f84e22d
num_stable_states_answer = md"""

Hi! ğŸª

"""

# â•”â•â•¡ 2495e330-2c0a-11eb-3a10-530f8b87a4eb
md"""
That's right, we have found a _bifurcation_!

#### Exercise 4.3

ğŸ‘‰ Make a bifurcation diagram. Create a plot with ``S`` on the horizontal axis, and ``T_{\text{equilibrium}}`` on the vertical. For a couple of pairs TODO
"""

# â•”â•â•¡ 59da0470-2b8f-11eb-098c-993effcedecf
# bifurcation_ST = [(S,T) for S in 1350:10:1600 for T in [-50, 0, 50]]

# bifurcation_ST = [(S,T) for S in 1350:10:1600 for T in [-50, 0, 50]]
# bifurcation_ST = [(S,T) for S in 1180:100:1680 for T in [-50, 0, 50]]

# â•”â•â•¡ 9f54c570-2b90-11eb-0e94-07e475a1908f
bifurcation_result = ThreadsX.map(bifurcation_ST) do p
	eq_T(p...)
end

# â•”â•â•¡ b0db7730-2b90-11eb-126b-33b04be4d686
scatter(
	first.(bifurcation_ST), bifurcation_result,
	label=nothing,
	xlabel="Solar insulation",
	ylabel="Equilibrium temperature",
	color=:black,
	) |> as_svg

# â•”â•â•¡ a04d3dee-2a9c-11eb-040e-7bd2facb2eaa
md"""
# Appendix
"""

# â•”â•â•¡ 0a6e6ad2-2c01-11eb-3151-3d58bc09bc69
ice_gradient = PlotUtils.ContinuousColorGradient([
		RGB(0.95, 0.95, 1.0), # sliver of white
		RGB(0.05, 0.0, 0.3), 
		RGB(0.1, 0.05, 0.4), 
		RGB(0.4, 0.4, 0.5), 
		RGB(0.95, 0.7, 0.4), 
		RGB(1.0, 0.9, 0.3)
	], [0.0, 0.001, 0.2, 0.5, 0.8, 1.0])

# â•”â•â•¡ c0e46442-27fb-11eb-2c94-15edbda3f84d
function plot_state(sim::ClimateModelSimulation; clims=(-1.1,1.1), 
		show_quiver=true, show_anomaly=false, IC=nothing)
	
	model = sim.model
	grid = sim.model.G
	
	
	p = plot(;
		xlabel="longitudinal distance [km]", ylabel="latitudinal distance [km]",
		clabel="Temperature",
		yticks=( (-grid.L:1000e3:grid.L), Int64.(1e-3*(-grid.L:1000e3:grid.L)) ),
		xticks=( (0:1000e3:grid.L), Int64.(1e-3*(0:1000e3:grid.L)) ),
		xlims=(0., grid.L), ylims=(-grid.L, grid.L),
		)
	
	X = repeat(grid.x, grid.Ny, 1)
	Y = repeat(grid.y, 1, grid.Nx)
	if show_anomaly
		arrow_col = :black
		maxdiff = maximum(abs.(sim.T .- IC))
		heatmap!(p, grid.x[:], grid.y[:], sim.T .- IC, clims=(-1.1, 1.1),
			color=:balance, colorbar_title="Temperature anomaly [Â°C]", linewidth=0.,
			size=(400,530)
		)
	else
		arrow_col = :white
		heatmap!(p, grid.x[:], grid.y[:], sim.T,
			color=ice_gradient, levels=clims[1]:(clims[2]-clims[1])/21.:clims[2],
			colorbar_title="Temperature [Â°C]", clims=clims,
			linewidth=0., size=(400,520)
		)
	end
	
	annotate!(p,
		50e3, 6170e3,
		text(
			string("t = ", Int64(round(sim.iteration*sim.Î”t/(60*60*24))), " days"),
			color=:black, :left, 9
		)
	)
	annotate!(p,
		3000e3, 6170e3,
		text(
			"mean(T) = $(round(mean(sim.T), digits=1)) Â°C",
			color=:black, :left, 9
		)
	)
	
	if show_quiver
		Nq = grid.N Ã· 5
		quiver!(p,
			X[(Nq+1)Ã·2:Nq:end], Y[(Nq+1)Ã·2:Nq:end],
			quiver=grid.L*4 .*(model.u[(Nq+1)Ã·2:Nq:end], model.v[(Nq+1)Ã·2:Nq:end]),
			color=arrow_col, alpha=0.7
		)
	end
	
	as_png(p)
end

# â•”â•â•¡ 3b24e1b0-2b46-11eb-383b-c57cbf3e68f1
let
	go_ex
	if ocean_sim.iteration == 0
		timestep!(ocean_sim)
	else
		for i in 1:50
			timestep!(ocean_sim)
		end
	end
	plot_state(ocean_sim, clims=(-10, 40), show_quiver=show_quiver, show_anomaly=show_anomaly, IC=ocean_T_init)
end

# â•”â•â•¡ 6568b850-2b4d-11eb-02e9-696654ac2d37
let
	go_radiation
	for i in 1:100
		timestep!(radiation_sim)
	end
	plot_state(radiation_sim; clims=(-0,50))
end

# â•”â•â•¡ 57b6e7d0-2c07-11eb-16c1-0d058a34c7ee
md"""
## **Exercise XX:** _Lecture transcript_
_(MIT students only)_

Please see the link for the transcript document on [Canvas](https://canvas.mit.edu/courses/5637).
We want each of you to correct about 500 lines, but donâ€™t spend more than 20 minutes on it.
See the the beginning of the document for more instructions.
:point_right: Please mention the name of the video(s) and the line ranges you edited:
"""

# â•”â•â•¡ 57c0d2e0-2c07-11eb-1091-15fec09c4e8b
lines_i_edited = md"""
Abstraction, lines 1-219; Array Basics, lines 1-137; Course Intro, lines 1-144 (_for example_)
"""

# â•”â•â•¡ 57cdcb30-2c07-11eb-39b2-2f225acf589d
if student.name == "Jazzy Doe" || student.kerberos_id == "jazz"
	md"""
	!!! danger "Before you submit"
	    Remember to fill in your **name** and **Kerberos ID** at the top of this notebook.
	"""
end

# â•”â•â•¡ 57e264a0-2c07-11eb-0e31-2b8fa01be2d1
md"## Function library

Just some helper functions used in the notebook."

# â•”â•â•¡ 57f57770-2c07-11eb-1720-cf00aa7f597b
hint(text) = Markdown.MD(Markdown.Admonition("hint", "Hint", [text]))

# â•”â•â•¡ 171c6880-2d0b-11eb-0180-454f2876cf51
hint(md"""
To measure the runtime of a Julia command, you can do:
```julia
runtime = @elapsed do_something()
```

To get a more precise benchmark, you can average a fixed number of runs, by putting `@elapsed` in front of a `for` loop, for example.
""")

# â•”â•â•¡ ec39a792-2bf7-11eb-11e5-515b39f1adf6
md"""
**How do you determine that the model has reached equilibrium?**

The simplest way is to use the interactive simulation above to find a fixed time/number of steps, for which any initial value has reached equilibrium. 

Using a long time period is more accurate, but it means that the runtime will be long. If this is a problem, you can use a dynamic stopping condition instead. For example, you can stop the simulation early when the total incoming and total outgoing radiation are roughly equal.
""" |> hint

# â•”â•â•¡ 58094d90-2c07-11eb-2987-15c068fefd8f
almost(text) = Markdown.MD(Markdown.Admonition("warning", "Almost there!", [text]))

# â•”â•â•¡ 581b9d10-2c07-11eb-1e60-c753aa19f4c3
still_missing(text=md"Replace `missing` with your answer.") = Markdown.MD(Markdown.Admonition("warning", "Here we go!", [text]))

# â•”â•â•¡ 582dc580-2c07-11eb-37e3-c32590a0c325
keep_working(text=md"The answer is not quite right.") = Markdown.MD(Markdown.Admonition("danger", "Keep working on it!", [text]))

# â•”â•â•¡ 58403c10-2c07-11eb-1f4b-f9ecb741d881
yays = [md"Fantastic!", md"Splendid!", md"Great!", md"Yay â¤", md"Great! ğŸ‰", md"Well done!", md"Keep it up!", md"Good job!", md"Awesome!", md"You got the right answer!", md"Let's move on to the next section."]

# â•”â•â•¡ 5853eb20-2c07-11eb-18bf-c14ed22ce153
correct(text=rand(yays)) = Markdown.MD(Markdown.Admonition("correct", "Got it!", [text]))

# â•”â•â•¡ 5867e850-2c07-11eb-17d5-9dac155d381b
not_defined(variable_name) = Markdown.MD(Markdown.Admonition("danger", "Oopsie!", [md"Make sure that you define a variable called **$(Markdown.Code(string(variable_name)))**"]))

# â•”â•â•¡ 587b9760-2c07-11eb-17ff-b9e950aa04ac
todo(text) = HTML("""<div
	style="background: rgb(220, 200, 255); padding: 2em; border-radius: 1em;"
	><h1>TODO</h1>$(repr(MIME"text/html"(), text))</div>""")



#html"<span style='display: inline; font-size: 2em; color: purple; font-weight: 900;'>TODO</span>"

# â•”â•â•¡ 13eb3966-2a9a-11eb-086c-05510a3f5b80
md"""
#### Data structures

Let's look at our first type, `Grid`. Notice that it only has one 'constructor function', which takes `N` (number of longitudinal grid points) and `L` (longitudinal size in meters) as arguments.

$(todo(md"talk about grid size, ghost cells"))
"""

# â•”â•â•¡ b19df5b0-2c05-11eb-0f59-83fa0aa6d0bb
md"""


Goals:

- play with the model, different initial temp conditions
- what is the effect of ...?
  - asdfasdf



- increase Î”t for better performance
- increase it too high -- find zebra pattern
- higher N needs smaller Î”t ğŸ‘‰ exercise 2

""" |> todo

# â•”â•â•¡ 87e59680-2d0c-11eb-03c7-1d845ca6a1a5
md"""
What you experienced is _numerical instability_ in our simulation method. This is not caused by floating point errors -- it is a theoretical limitation of our method.

To ensure the stability of our finite-difference approximation for advection, heat should not be displaced more than one grid cell in a single timestep. Mathematically, we can ensure this by checking that the distance $L_{CFL} \equiv \max(|\vec{u}|) \Delta t$ is less than the width $\Delta x = \Delta y$ of a single grid cell:

$L_{CFL} \equiv \max(|\vec{u}|) \Delta t < \Delta x$

or 

$\Delta t  < \frac{\Delta x}{\max(|\vec{u}|) },$

which is known as **the Courant-Freidrichs-Levy (CFL) condition**. This inequality states that if we want to decrease the grid spacing $\Delta x$ (or increase the *resolution* $N_{x}$), we also have to decrease the timestep $\Delta t$ by the same factor. In other words, the timestep can not be thought of as fixed an in fact also depends on the spatial resolution: $\Delta t \equiv \Delta t_{0} N_{x}$.

Revisiting our complexity equation, we now have

$\mathcal{O}(T(t_{M})) = \mathcal{O}(M) * \mathcal{O}(\Delta t) * \mathcal{O}(\text{tendencies}) = \mathcal{O}(M) \mathcal{O}(N_{x}^{3}),$

ğŸ‘‰ EXERCISE: VERIFY THAT THIS IS TRUE BY SETTING $\Delta t = 0.1 \frac{\Delta x}{\max(|\vec{u}|) }$ TO ENSURE THE CFL CONDITION ALWAYS HOLDS

$(todo(md"We need to think about how this exercise actually _verifies_ the condition, instead of just implementing it. They need to find that the same Î”t can be stable/unstable, depending on N."))
"""

# â•”â•â•¡ e1d01f70-2d0d-11eb-1367-5b5305d94774
md"""

To Henri:

I thought that we assume that we want to simulate a fixed amount of time, but depending on Î”t, this will require a variable number of steps, M.


So $M = O(1 / Î”t)$, right? And $Î”t = O(1 / N)$, (not ``O(N)``), 

So, in 3D:

``runtime = O(M)O(N^3) = O(N^4)``

""" |> todo

# â•”â•â•¡ fced660c-2cd9-11eb-1737-0110789f429e
md"""
compare to Moore's Law

""" |> todo

# â•”â•â•¡ 4cba7260-2c08-11eb-0a81-abdff2f867de
md"""
## **Exercise 3** - Adding radiation to our ocean model to build a 2-D climate model

In Homework 9, we used a **zero-dimensional (0-D)** Energy Balance Model (EBM) to understand how Earth's average radiative imbalance results in temperature changes:

$C\frac{\partial T}{\partial t} = \frac{S(1 - \alpha)}{4} - (A - BT)$

This week we will do the same, but now in **two-dimensions (2-D)**, where in addition to heat being added or removed from the system by radiation, heat can be *transported around the system* by oceanic **advection** and **diffusion** (see also Lectures 22 & 23 for 1-D and 2-D advection-diffusion). The governing equation for temperature $T(x,y,t)$ in our coupled climate model is:

$\begin{split}
\frac{\partial T}{\partial t} &= 
u(x,y) \frac{\partial T}{\partial x} + v(x,y) \frac{\partial T}{\partial y} 
&\qquad\text{(advection)}
\\

& + \kappa \left( \frac{\partial^{2} T}{\partial x^{2}} + \frac{\partial^{2} T}{\partial y^{2}} \right) 
&\qquad\text{(diffusion)}
\\

& + \frac{S(x,y)(1 - \alpha(x,y))}{4C} 
&\qquad\text{(absorbed radiation)}
\\

& - \frac{(A - BT)}{C}. 
&\qquad\text{(outgoing radiation)}
\end{split}$

### What we will give:
- The struct `RadiationOceanModelParameters` below, with our tuned initial values
- The 0D functions for absorbed and outgoing radiation. With demonstrations

### What they will write:
- The 2D methods for absorbed and outgoing radiation, with signature `(T::Array{Float64,2}, model::RadiationOceanModel)`
- The `timestep!(sim::ClimateModelSimulation{RadiationOceanModel})` method
- 



""" |> todo

# â•”â•â•¡ 8de8dda0-2d0f-11eb-105b-9d8779275e6c
md"""

We still need to find more realistic initial values. Right now, the contrast between pole and equator is too large, maybe more adv + diffusion, and then we need to re-tune the rest to have three stable states at S=1380

""" |> todo

# â•”â•â•¡ 8b5a22f0-2b8f-11eb-094f-c5ceb1842998
md"""
## **Exercise 4** - _Bifurcation diagram_



""" |> todo

# â•”â•â•¡ Cell order:
# â•Ÿâ”€67c3dcc0-2c05-11eb-3a84-9dfea24f95a8
# â•Ÿâ”€6a4641e0-2c05-11eb-3430-6f14650c2ad3
# â•Ÿâ”€621230b0-2c05-11eb-2a98-5bd1d7be9038
# â• â•6cb238d0-2c05-11eb-221e-d5df4c479302
# â•Ÿâ”€70077e50-2c05-11eb-3d83-732b4b780d04
# â• â•9c8a7e5a-12dd-11eb-1b99-cd1d52aefa1d
# â•Ÿâ”€ed741ec6-1f75-11eb-03be-ad6284abaab8
# â•Ÿâ”€68c01d90-2cf6-11eb-0771-7b3c6db89ecb
# â•Ÿâ”€295af330-2cf8-11eb-1606-437e8f3c43fd
# â•Ÿâ”€c33ebe40-2cf9-11eb-384c-432dc70497b0
# â•Ÿâ”€83ad05a0-2cfb-11eb-1467-e1196985519a
# â• â•f4c884fc-2a97-11eb-1ba9-01bf579f8b43
# â• â•1e8d37ee-2a97-11eb-1d45-6b426b25d4eb
# â• â•682f2530-2a97-11eb-3ee6-99a7c79b3767
# â• â•ee6716c8-2a95-11eb-3a00-319ee69dd37f
# â• â•b629d89a-2a95-11eb-2f27-3dfa45789be4
# â• â•a8d8f8d2-2cfa-11eb-3c3e-d54f7b32e4a2
# â• â•13eb3966-2a9a-11eb-086c-05510a3f5b80
# â• â•cd2ee4ca-2a06-11eb-0e61-e9a2ecf72bd6
# â• â•e7f563f0-2d04-11eb-036d-992da68470a6
# â• â•39404240-2cfe-11eb-2e3c-710e37f8cd4b
# â• â•0d63e6b2-2b49-11eb-3413-43977d299d90
# â• â•32663184-2a81-11eb-0dd1-dd1e10ed9ec6
# â• â•d3796644-2a05-11eb-11b8-87b6e8c311f9
# â• â•5f5e4120-2cfe-11eb-1fa7-99fdd734f7a7
# â• â•74aa7512-2a9c-11eb-118c-c7a5b60eac1b
# â• â•f92086c4-2a74-11eb-3c72-a1096667183b
# â• â•81bb6a4a-2a9c-11eb-38bb-f7701c79afa2
# â• â•7caca2fa-2a9a-11eb-373f-156a459a1637
# â•Ÿâ”€31cb0c2c-2a9a-11eb-10ba-d90a00d8e03a
# â• â•9841ff20-2c06-11eb-3c4c-c34e465e1594
# â•Ÿâ”€31cb7aae-2d04-11eb-30cb-a365a6a4aa6b
# â• â•1dd3fc70-2c06-11eb-27fe-f325ca208504
# â•Ÿâ”€50e89130-2d04-11eb-1c8e-a34775aec40c
# â• â•6f19cd80-2c06-11eb-278d-178c1590856f
# â•Ÿâ”€c7736640-2d04-11eb-1108-59a7446e244d
# â• â•863a6330-2a08-11eb-3992-c3db439fb624
# â•Ÿâ”€981ef38a-2a8b-11eb-08be-b94be2924366
# â•Ÿâ”€d042d25a-2a62-11eb-33fe-65494bb2fad5
# â•Ÿâ”€6dbc3d34-2a89-11eb-2c80-75459a8e237a
# â•Ÿâ”€c20b0e00-2a8a-11eb-045d-9db88411746f
# â•Ÿâ”€933d42fa-2a67-11eb-07de-61cab7567d7d
# â•Ÿâ”€c9ea0f72-2a67-11eb-20ba-376ca9c8014f
# â•Ÿâ”€3b24e1b0-2b46-11eb-383b-c57cbf3e68f1
# â•Ÿâ”€c3f086f4-2a9a-11eb-0978-27532cbecebf
# â•Ÿâ”€bff89550-2a9a-11eb-3038-d70249c96219
# â•Ÿâ”€dc9d12d0-2a9a-11eb-3dae-85b3b6029658
# â•Ÿâ”€c0298712-2a88-11eb-09af-bf2c39167aa6
# â•Ÿâ”€e2e4cfac-2a63-11eb-1b7f-9d8d5d304b43
# â•Ÿâ”€e3ee80c0-12dd-11eb-110a-c336bb978c51
# â•Ÿâ”€df706ebc-2a63-11eb-0b09-fd9f151cb5a8
# â•Ÿâ”€bb084ace-12e2-11eb-2dfc-111e90eabfdd
# â•Ÿâ”€ecaab27e-2a16-11eb-0e99-87c91e659cf3
# â•Ÿâ”€e59d869c-2a88-11eb-2511-5d5b4b380b80
# â•Ÿâ”€0ae0bb70-2b8f-11eb-0104-93aa0e1c7a72
# â•Ÿâ”€c4424838-12e2-11eb-25eb-058344b39c8b
# â•Ÿâ”€3d12c114-2a0a-11eb-131e-d1a39b4f440b
# â•Ÿâ”€6b3b6030-2066-11eb-3343-e19284638efb
# â• â•b19df5b0-2c05-11eb-0f59-83fa0aa6d0bb
# â•Ÿâ”€88c56350-2c08-11eb-14e9-77e71d749e6d
# â•Ÿâ”€014495d6-2cda-11eb-05d7-91e5a467647e
# â•Ÿâ”€d6a56496-2cda-11eb-3d54-d7141a49a446
# â• â•126bffce-2d0b-11eb-2bfd-bb5d1ad1169b
# â• â•8346b590-2b41-11eb-0bc1-1ba79bb77dfb
# â•Ÿâ”€171c6880-2d0b-11eb-0180-454f2876cf51
# â•Ÿâ”€923af680-2d0b-11eb-3f6a-db4bf29bb6a9
# â• â•b50d25c0-2d0b-11eb-3b0c-1b178bbddecd
# â• â•16905a6a-2a78-11eb-19ea-81adddc21088
# â• â•a8136ed0-2d0e-11eb-01b3-4101bd813faf
# â• â•794c2148-2a78-11eb-2756-5bd28b7726fa
# â•Ÿâ”€a6811db2-2cdf-11eb-0aac-b1bf7b7d99eb
# â• â•87de1c70-2d0c-11eb-2c22-f76eeca58f33
# â•Ÿâ”€87e59680-2d0c-11eb-03c7-1d845ca6a1a5
# â• â•ad7b7ed6-2a9c-11eb-06b7-0f5595167575
# â•Ÿâ”€e1d01f70-2d0d-11eb-1367-5b5305d94774
# â•Ÿâ”€433a9c1e-2ce0-11eb-319c-e9c785b080ce
# â•Ÿâ”€213f65ce-2ce1-11eb-19d6-5bf5c24d7ed7
# â• â•fced660c-2cd9-11eb-1737-0110789f429e
# â•Ÿâ”€d9e23a5a-2a8b-11eb-23f1-73ff28be9f12
# â• â•4cba7260-2c08-11eb-0a81-abdff2f867de
# â• â•545cf530-2b48-11eb-378c-3f8eeb89bcba
# â• â•57535c60-2b49-11eb-07cc-ffc5b4d1f13c
# â• â•ef902590-2bf7-11eb-1eb0-712b3eb3f7c1
# â• â•90e1aa00-2b48-11eb-1a2d-8701a3069e50
# â•Ÿâ”€de7456c0-2b4b-11eb-13c8-01b196821de4
# â• â•6745f610-2b48-11eb-2f6c-79e0009dc9c3
# â• â•a033fa20-2b49-11eb-20e0-5dd968b0c0c6
# â• â•6c20ca1e-2b48-11eb-1c3c-418118408c4c
# â• â•e80b0532-2b4b-11eb-26fa-cd09eca808bc
# â• â•42bb2f70-2b4a-11eb-1637-e50e1fad45f3
# â• â•b99b5b00-2b4b-11eb-260e-21363d1f4a9b
# â• â•a3e524d0-2b55-11eb-09e2-25a968d79640
# â• â•388898b0-2b56-11eb-2537-c596394b9e20
# â• â•629454e0-2b48-11eb-2ff0-abed400c49f9
# â• â•d63c5fe0-2b49-11eb-07fd-a7ec98af3a89
# â• â•f2e2f820-2b49-11eb-1c6c-19ae8157b2b9
# â•Ÿâ”€fe492480-2b4b-11eb-050e-9b9b2e2bf50f
# â• â•068795ee-2b4c-11eb-3e58-353eb8978c1c
# â•Ÿâ”€ad95c4e0-2b4a-11eb-3584-dda89970ffdf
# â• â•8de8dda0-2d0f-11eb-105b-9d8779275e6c
# â• â•b059c6e0-2b4a-11eb-216a-39bb43c7b423
# â•Ÿâ”€5fd346d0-2b4d-11eb-066b-9ba9c9d97613
# â•Ÿâ”€6568b850-2b4d-11eb-02e9-696654ac2d37
# â• â•50c6d850-2b57-11eb-2330-1d1547219b5e
# â• â•57dcf660-2b57-11eb-1518-b7e2e65abfcc
# â• â•f5010a40-2b56-11eb-266a-a71b92692172
# â• â•ef647620-2c01-11eb-185e-3f36f98fcfaf
# â•Ÿâ”€8b5a22f0-2b8f-11eb-094f-c5ceb1842998
# â•Ÿâ”€127bcb0e-2c0a-11eb-23df-a75767910fcb
# â• â•c40870d0-2b8e-11eb-0fa6-d7fcb1c6611b
# â•Ÿâ”€ec39a792-2bf7-11eb-11e5-515b39f1adf6
# â•Ÿâ”€2ef83140-2d16-11eb-1d8c-a13048a8e04f
# â•Ÿâ”€2ae47330-2d15-11eb-1d2e-55343fa3b01a
# â• â•5294aad0-2d15-11eb-091d-59d7517c4dc2
# â• â•abd2475e-2d15-11eb-26dc-05253cf65232
# â• â•bdd86250-2d15-11eb-0b62-7903ca714312
# â• â•bed2c7e0-2d15-11eb-14e3-93f5d1b6f3a1
# â•Ÿâ”€c40b7360-2d15-11eb-0558-d95d615f9b9b
# â• â•02767460-2d16-11eb-2074-07657f84e22d
# â• â•2495e330-2c0a-11eb-3a10-530f8b87a4eb
# â• â•59da0470-2b8f-11eb-098c-993effcedecf
# â• â•9f54c570-2b90-11eb-0e94-07e475a1908f
# â• â•b0db7730-2b90-11eb-126b-33b04be4d686
# â•Ÿâ”€a04d3dee-2a9c-11eb-040e-7bd2facb2eaa
# â• â•c0e46442-27fb-11eb-2c94-15edbda3f84d
# â• â•0a6e6ad2-2c01-11eb-3151-3d58bc09bc69
# â•Ÿâ”€57b6e7d0-2c07-11eb-16c1-0d058a34c7ee
# â• â•57c0d2e0-2c07-11eb-1091-15fec09c4e8b
# â•Ÿâ”€57cdcb30-2c07-11eb-39b2-2f225acf589d
# â•Ÿâ”€57e264a0-2c07-11eb-0e31-2b8fa01be2d1
# â•Ÿâ”€57f57770-2c07-11eb-1720-cf00aa7f597b
# â•Ÿâ”€58094d90-2c07-11eb-2987-15c068fefd8f
# â•Ÿâ”€581b9d10-2c07-11eb-1e60-c753aa19f4c3
# â•Ÿâ”€582dc580-2c07-11eb-37e3-c32590a0c325
# â•Ÿâ”€58403c10-2c07-11eb-1f4b-f9ecb741d881
# â•Ÿâ”€5853eb20-2c07-11eb-18bf-c14ed22ce153
# â•Ÿâ”€5867e850-2c07-11eb-17d5-9dac155d381b
# â• â•587b9760-2c07-11eb-17ff-b9e950aa04ac
